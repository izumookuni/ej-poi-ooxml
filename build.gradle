import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.9.8'
}

group 'cc.domovoi'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "${artifactory_url}"
        credentials {
            username "${artifactory_user}"
            password "${artifactory_password}"
        }
    }
    maven {
        url 'https://dl.bintray.com/izumookuni/domovoi/'
    }
}

dependencies {
    
    compile group: 'cc.domovoi', name: 'ej-collection', version: '0.0.2'
    compile group: 'cc.domovoi', name: 'ej-lambda', version: '0.0.2'

    compile group: 'com.typesafe', name: 'config', version: '1.3.3'
    compile group: 'org.jooq', name: 'joor-java-8', version: '0.9.11'
    
    compile group: 'org.apache.poi', name: 'poi', version: '4.1.0'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.0'
    compile group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '4.1.0'

    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-access', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId "${project.group}"
            artifactId "${project.name}"
//            version "${project.version}"
            version "${LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMddHHmmss"))}-SNAPSHOT"
        }
    }
}

artifactory {
    contextUrl = "${artifactory_url}"
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications ('mavenJava')
//            properties = ['qa.level': 'basic', 'q.os': 'win32, deb, osx']
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
            publishIvy = false
        }
    }
//    resolve {
//        repository {
//            repoKey = 'libs-release'
//            username = "${artifactory_user}"
//            password = "${artifactory_password}"
//            maven = true
//        }
//    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}
